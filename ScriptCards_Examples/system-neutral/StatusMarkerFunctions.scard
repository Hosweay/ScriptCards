!script {{
  --/|Script Name : StatusMarkerFunctions
  --/|Version     : 1.0.1
  --/|Requires SC : 2.1.7
  --/|Author      : Kurt Jaegers

  --/|IMPORTANT NOTE: THIS IS NOT A STANDALONE SCRIPT. This script provides a set of functions that can be added to
  --/|                your own scripts or to a ScriptCards library in order to aid in dealing with the Roll20 Status Markers.
  
  --/|Functions     : The following functions are meant to be called by your script.
  --/|
  --/|                CHECK_STATUS_MARKER - TokenID;MarkerToFind;ExistsVariable;CountVariable
  --/|                     Will check the given token (TokenID) for the status marker (MarkerToFind) and set two return
  --/|                     string variables: (ExistsVariable) will be 1 if the token has the marker, and (CountVariable)
  --/|                     will contain the counter number on the marker (default 0 for no number)
  --/||
  --/|                REMOVE_STATUS_MARKER - TokenID;MarkerToRemove
  --/|                     Will remove any variation of (MarkerToRemove from the given token (TokenID)
  --/|
  --/|                ADD_STATUS_MARKER - TokenID;MarkerToAdd;Count
  --/|                     Will add (MarkerToAdd) to the given token (TokenID). If count is specified with a number
  --/|                     betweeen 1 and 9, the number will be placed on top of the status marker.
  --/|                DECREMENT_STATUS_MARKER - TokenID;MarkerToDecrement;Blank, 0 or 1 (OPTIONAL)
  --/|                     If (MarkerToDecrement) exists on the on token (TokenID), the counter number will be
  --/|                     decreased by 1. If a third parameter is added and set to 1, the status marker will
  --/|                     be removed when decrementing from 1 to no counter. If set to 0, the marker will be
  --/|                     be removed when decrementing for a marker without a counter. If not specified, the
  --/|                     marker will not be removed when the counter would go below 1 or 0.
  --/|
  --/|                CLEAR_STATUS_MARKERS - TokenID
  --/|                     Removes all status markers from the indicated token.
  
  --/|The commented out lines below are just samples of calling some of the functions in this script.
  
  --/|Remove all status markers from the selected token
  --/>CLEAR_STATUS_MARKERS|@{selected|token_id}

  --/|Add the "snail" status marker with a counter of 5 to the selected token
  --/>ADD_STATUS_MARKER|@{selected|token_id};snail;5

  --/|Decrement the counter on the "snail" status marker on the selected token, removing it if the number goes below 1
  --/>DECREMENT_STATUS_MARKER|@{selected|token_id};snail;1

  --X|

  --:ADD_STATUS_MARKER|Token_ID;Status Marker;Count
  -->REMOVE_STATUS_MARKER|[%1%];[%2%]
  --~|array;statusmarkers;Conditions;[%1%]
  --&toAdd|[%2%]
  --?"[%2%]" -ne "dead" -and "X[%3%]" -ne "X" -and "X[%3%]" -ne "X0"|=countToAdd;[%3%] {MIN:0} {MAX:9}
  --?"[%2%]" -ne "dead" -and "X[%3%]" -ne "X" -and "X[%3%]" -ne "X0"|&toAdd;[%2%]@[$countToAdd.Raw]
  --~|array;add;Conditions;[&toAdd]
  --~newConditions|array;stringify;Conditions
  --#parameterdelimiter|$
  --~newConditions|string$replaceall$;$,$[&newConditions]
  --#parameterdelimiter|;
  --?"[&newConditions(0,1)]" -eq ","|&newConditions;[&newConditions(1)]
  --!t:[%1%]|statusmarkers:[&newConditions]
  --:DONE_ADD_STATUS_MARKER|
  --<|

  --:REMOVE_STATUS_MARKER|Token_ID;Status Marker
  -->CHECK_STATUS_MARKER|[%1%];[%2%];MarkerExists;MarkerCounter  
  --?[&MarkerExists] -eq 0|<
  --~|array;statusmarkers;Conditions;[%1%]
  --?[&MarkerCounter] -eq 0|&toCheckFor;[%2%]|&toCheckFor;[%2%]@[&MarkerCounter]
  --~hasCondition|array;indexof;Conditions;[&toCheckFor]
  --?[&hasCondition] -eq "ArrayError"|DONE_REMOVE_STATUS_MARKER
  --~|array;remove;Conditions;[&toCheckFor]
  --~newConditions|array;stringify;Conditions
  --#parameterdelimiter|$
  --~newConditions|string$replaceall$;$,$[&newConditions]
  --#parameterdelimiter|;
  --!t:[%1%]|statusmarkers: [&newConditions]
  --:DONE_REMOVE_STATUS_MARKER|
  --<|  

  --:DECREMENT_STATUS_MARKER|Token_ID;Status Marker;Optional:0=remove marker if decrementing from 0 or 1=Remove marker if decrementing TO 0.
  -->CHECK_STATUS_MARKER|[%1%];[%2%];_DEC_EXIST;_DEC_COUNT
  --?[&_DEC_EXIST] -eq 0|DONE_DECREMENT_STATUS_MARKER
  --?[&_DEC_COUNT] -eq 0 -and "X[%3%]" -eq "X"|DONE_DECREMENT_STATUS_MARKER
  -->REMOVE_STATUS_MARKER|[%1%];[%2%]
  --?[&_DEC_COUNT] -eq 1 -and "X[%3%]" -eq "X1"|DONE_DECREMENT_STATUS_MARKER
  --?[&_DEC_COUNT] -eq 0 -and "X[%3%]" -eq "X0"|DONE_DECREMENT_STATUS_MARKER
  --?[&_DEC_COUNT] -gt 1|>ADD_STATUS_MARKER;[%1%];[%2%];[=[&_DEC_COUNT]-1]
  --?[&_DEC_COUNT] -eq 1|>ADD_STATUS_MARKER;[%1%];[%2%]
  --:DONE_DECREMENT_STATUS_MARKER|
  --<|
  
  --:CHECK_STATUS_MARKER|TokenID;MarkerToFind;Exists;Counter
  --~|array;statusmarkers;Conditions;[%1%]
  --&FoundMarker|-1
  --%loop|0;[@Conditions(maxindex)]
    --&Temp|[@Conditions([&loop])]
	--?[&Temp(indexof,[%2%])] -eq 0|&FoundMarker;[&loop]
  --%|
  --?[&FoundMarker] -ne -1|FOUND_STATUS_MARKER
  --&[%3%]|0 --&[%4%]|0 --<|
  --:FOUND_STATUS_MARKER|
  --&[%3%]|1
  --&[%4%]|0
  --&ThisMarker|[@Conditions([&FoundMarker])]
  --?[&ThisMarker(contains,@)] -eq 0|<
  --~[%4%]|string;after;@;[@Conditions([&FoundMarker])]
  --<|

  --:CLEAR_STATUS_MARKERS|TokenID
  --!t:[%1%]|statusmarkers:|
  --<|
 }}