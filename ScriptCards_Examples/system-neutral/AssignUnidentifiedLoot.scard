!script {{
  --/|Script Name : AssignUnidentifiedLoot
  --/|Version     : 1.0.0
  --/|Requires SC : 2.1.7
  --/|Author      : Kurt Jaegers

  --/|This script allows you to define "loot tokens" with GMNotes entries crafted to represent unidentified magic items.
  --/|Running this script with the loot token selected will create buttons to distribute that loot to individual characters,
  --/|which is done by appending the item name in the GMNotes section of the character's token.
  --/|
  --/|The loot token should be structured to have lines beginning with "Item: " followed by the item name (the space after)
  --/|the colon is required or the items name will be cut off. Items should be entered one per line, hitting enter after each one.
  
  --#reentrant|DistributeLoot@{selected|token_id}
  --#whisper|self
  --/|Make sure the player running the script is a GM on the game
  --?[&SendingPlayerIsGM] -eq 0|GMONLY

  --/|Make sure a token is selected
  --?"X@{selected|token_id}" -eq "X"|NEEDSELECTED

  --/|Define some replacements we will need later
  --&CR|< --&CR|+br --&CR|+ />
  --&TB|& --&TB|+# --&TB|+64 --&TB|+; --&TB|+{
  --&SPACE|X X --&SPACE|[&SPACE(1,1)]

  --/|Read the token's GM Notes field and replace </p> with a vertical bar (|) so we can separate the list into items
  --/|also, strip out some other HTML items.
  --&LootToken|@{selected|token_id}
  --&LootItems|[*[&LootToken]:t-gmnotes]
  --&LootItems|[&LootItems(replaceall,%20, )]
  --&LootItems|[&LootItems(replaceall,%3A,:)]
  --&LootItems|[&LootItems(replaceall,%3Cp%3E,)]
  --&LootItems|[&LootItems(replaceall,%3C/p%3E,|)]
  --&LootItems|[&LootItems(replaceall,%3Cbr%3E,)]
  
  --/|Split the notes into an array
  --~|array;fromstring;LootList;|;[&LootItems]
  --&LootButtons|
  --=ItemCount|0
  
  --/|Create a list of buttons
  --%loop|foreach;LootList
    --?"X[&loop]" -inc "XItem:"|[
    --&ThisItem|[&loop(6)]
    --&LootButtons|+[rbutton][&ThisItem]::ASSIGN_LOOT_ITEM;[&ThisItem]|[&TB]target|token_id}[/rbutton][&SPACE][BR]
    --=ItemCount|[$ItemCount] + 1
    --]|
  --%|

  --/|Abort if there are no loot items in the notes
  --?[$ItemCount] -eq 0|NOLOOTITEMS

  --/|Show the buttons
  --+Assign Loot|
  --+|[&LootButtons]
  --X|

  --:ASSIGN_LOOT_ITEM|
  --~ItemName|string;before;|;[&reentryval]
  --~LootTarget|string;after;|;[&reentryval]
  --!t:[&LootTarget]|gmnotes:+=[&CR]Looted Item: [&ItemName]
  --+[*[&LootTarget]:t-name] assigned|[&ItemName]
  --X|

  --:GMONLY|
  --+GM ONLY|Only the GM can run this script
  --X|

  --:NEEDSELECTED|
  --+No token selected|You need to select a loot token
  --X|

  --:NOLOOTITEMS|
  --+No Loot Items|The selected token's GMNotes does not contain any lines beginning with "Item:"
  --X|
}}

