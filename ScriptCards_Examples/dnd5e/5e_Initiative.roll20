!script {{
  --/|Script Name : 5E Initiative Roller
  --/|Version     : 1.0
  --/|Requires SC : 1.6.2+ (better with 1.6.4 or higher)
  --/|Author      : Kurt Jaegers

  --/|Description : Handles rolling initiative for selected token(s).

  --/|================ Customize the behavior of the script by setting these values ================
  
  --/|If set to 1, the script will always ADD a token entry in the turn tracker instead of replacing the existing token
  --/|If left at the default (0) the token in the tracker will be replaced/updated if it already exists.
  --&alwaysAddExtraToken|0

  --/|If set to 1, the turn tracker won't be modified
  --&dontUpdateTurnTracker|0

  --/|The script title
  --#title|Initiative
      
  --/|===================== There are no customization options below this line =====================

  --/|We need these substitution variable to get around the 5E sheet's weirdness about using dice rolls that look like attributes
  --&obrac|{
  --&cbrac|}
  --&at|@

  --~selectedTokens|getselected
    --%loop|1;[&selectedTokensCount]
    --&thisId|[&selectedTokens[&loop]]
    --&initDie|1d20
    --?"[*[&thisId]:initiative_style]" -eq "[&obrac][&at][&obrac]d20[&cbrac],[&at][&obrac]d20[&cbrac][&cbrac]kh1"|&initDie;2d20kh1
    --?"[*[&thisId]:initiative_style]" -eq "[&obrac][&at][&obrac]d20[&cbrac],[&at][&obrac]d20[&cbrac][&cbrac]kl1"|&initDie;2d20kl1
    --&initBonus|0
    --?"X[*[&thisID]:initiative_bonus]" -ne "X"|&initBonus;[*[&thisID]:initiative_bonus]
    --=initRoll|[&initDie] + [&initBonus]
    --+[*[&thisId]:t-name]|[$initRoll]
    --&method1|dontremovetoken
    --&method2|replacetoken
    --?"[&ScriptCards_Version]X" -eq "X"|&method1;removetoken
    --?"[&ScriptCards_Version]X" -eq "X"|&method2;addtoken
    --?[&alwaysAddExtraToken] -eq 1|&method1;dontremovetoken
    --?[&alwaysAddExtraToken] -eq 1|&method2;addtoken
    --?[&dontUpdateTurnTracker] -eq 1|&method1;dontremovetoken
    --?[&dontUpdateTurnTracker] -eq 1|&method2;dontaddtoken
    --~|turnorder;[&method1];[&thisId]
    --~|turnorder;[&method2];[&thisId];[$initRoll.Raw]
  --%|
  }}