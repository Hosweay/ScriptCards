!script {{
  --/|Script Name : 5E Dragonborn Breath
  --/|Version     : 1.0
  --/|Requires SC : 1.4.0+, alterbars or token-mod to apply damage
  --/|Author      : Kurt Jaegers

  --/|Description : Handles a dragonborn's draconic breath weapon. IF your character was created with the 
  --/|              charactermancer, you don't need to do anything. If it wasn't, you'll need to add a 
  --/|              "draconicheritage" attribute to the character with the dragon type (black, red, green, etc.)
  --/|              listed in it. If all else fails, a black dragon's acid breath will be assumed.
  
  --/|Set this to either "token-mod", "alterbars", or anything else to not apply damage
  --&damageApplyScript|token-mod

  --/|Set this to the bar number you use to track hit points
  --&hitPointsBar|3

  --/|===================== There are no customization options below this line =====================
  --&damageType|Acid
  --&damageShape|Line
  --&breathSaveType|Dexterity
  --&spellName|Dragonborn [&damageType] Breath
  --#sourcetoken|@{selected|token_id}
  --&damageSides|6
  --&damageDice|2
  --=breathSaveDCTemp|8 + [*S:constitution_mod] + [*S:pb]
  --&breathSaveDC|[$breathSaveDCTemp.Raw]
  --&breathEffect|burn-acid

  --/|Account for the number of damage dice based on the character's level (2d6 at 1st, 3d6 at 4th, 4d6 at 11th, and 5d6 at 16th)
  --?[*S:level] -gt 6|&damageDice;3
  --?[*S:level] -gt 11|&damageDice;4
  --?[*S:level] -gt 16|&damageDice;5
  
  --/|Attempts to determine the dragon type. If the characeter wasn't built with the charactermancer, this will fail. In that case,
  --/|edit the two items at the top of the script (damageType and damageShape)
  --&work|Unknown
  --RFind|[*S:character_id];Breath Weapon;repeating_traits;name
  --?"[*R:name]" -ne "NoRepeatingAttributeLoaded"|[
  --~work|string;before; draconic ;[*R:description]
  --~work|string;after;. Your ;[&work]
  --~work|string;tolowercase;[&work]
  --]|

  --?[&work] -ne "Unknown"|doneDetermineType
  --?"X[*S:draconicheritage]X" -ne "XX"|doReadAttribute
  --&work|black
  -->doneDetermineType|

  --:doReadAttribute|
  --~work|string;tolowercase;[*S:draconicheritage]

  --:doneDetermineType|

  --C[&work]|black:&damageType;Acid|blue:&damageType;Lightning|brass:&damageType;Fire|bronze:&damageType;Lightning|copper:&damageType;Acid|gold:&damageType;Fire|green:&damageType;Poison|red:&damageType;Fire|silver:&damageType;Cold|white:&damageType;Cold
  --C[&work]|black:&breathEffect;burn-acid|blue:&breathEffect;burn-holy|brass:&breathEffect;burn-Fire|bronze:&breathEffect;burn-holy|copper:&breathEffect;burn-acid|gold:&breathEffect;burn-fire|green:&breathEffect;burn-slime|red:&breathEffect;burn-fire|silver:&breathEffect;burn-frost|white:&breathEffect;burn-frost
  --&damageShape|Line
  --C[&work]|gold:&damageShape;Cone|green:&damageShape;Cone|red:&damageShape;Cone|silver:&damageShape;Cone|white:&damageShape;Cone
  --&breathSaveType|Dexterity
  --C[&work]|green:&breathSaveType;Constitution|silver:&breathSaveType;Constitution|white:&breathSaveType;Constitution

  --~saveAttributeName|string;tolowercase;[&breathSaveType]_save_bonus

  --/|Prompt for a direction for the breath, and define arrays that indicate the relative spaces that will be hit.
  --&direction|?{Direction|&#x2B06; (UP),U|&#x2197; (UP-RIGHT),UR|&#x27A1; (RIGHT),R|&#x2198; (DOWN-RIGHT),DR|&#x2B07; (DOWN),D|&#x2199; (DOWN-LEFT),DL|&#x2B05; (LEFT),L|&#x2196 (UP-LEFT);,UL|}
  --?[&damageShape] -eq "Cone"|&damageDirection;C[&direction]
  --?[&damageShape] -eq "Line"|&damageDirection;L[&direction]
  --/+Direction|[&direction]
  --/+Damage Dir|[&damageDirection]
  --~|array;define;CU;0,-1;0,-2;-1,-2;-1,-3;0,-3;1,-3
  --~|array;define;CD;0,1;0,2;1,2;-1,3;0,3;1,3
  --~|array;define;CR;1,0;2,0;2,-1;3,-1;3,0;3,1
  --~|array;define;CL;-1,0;-2,0;-2,-1;-3,-1;-3,0;-3,1
  --~|array;define;CUR;1,-1;1,-2;1,-3;2,-1;2,-2;3,-1
  --~|array;define;CDR;1,1;1,2;1,3;2,1;2,2;3,1
  --~|array;define;CUL;-1,-1;-1,-2;-1,-3;-2,-1;-2,-2;-3,-1
  --~|array;define;CDL;-1,1;-1,2;-1,3;-2,1;-2,2;-3,1
  --~|array;define;LU;0,-1;0,-2;0,-3;0,-4;0,-5;0,-6
  --~|array;define;LD;0,1;0,2;0,3;0,4;0,5;0,6
  --~|array;define;LL;-1,0;-2,0;-3,0;-4,0;-5,0;-6,0
  --~|array;define;LR;1,0;2,0;3,0;4,0;5,0;6,0
  --~|array;define;LUL;-1,-1;-2,-2;-3,-3;-4,-4;-5,-5;-6,-6
  --~|array;define;LUR;1,-1;2,-2;3,-3;4,-4;5,-5;6,-6
  --~|array;define;LDL;-1,1;-2,2;-3,3;-4,4;-5,5;-6,6
  --~|array;define;LDR;1,1;2,2;3,3;4,4;5,5;6,6

  --/|Get all of the tokens on the page so we can cache their positions
  --~|array;pagetokens;alltokens;@{selected|token_id};char
  -->createTokenLookup|

  --#leftsub|Save DC [&breathSaveDC]
  --#rightsub|Save Type [&breathSaveType]
  --#title|[&damageType] Dragonborn Breath

  --/|Calculate damage
  --=Damage|[&damageSides]d[&damageDice]
  --=HalfDamage|[$Damage.Total] \ 2
  --=DoubleDamage|[$Damage.Total] * 2
  --=QuarterDamage|[$Damage.Total] \ 4

  --/|Determine damage subroutine
  --&damageRoutine|DontApplyDamage
  --?[&damageApplyScript] -eq "token-mod"|&damageRoutine;ApplyDamageTokenmod
  --?[&damageApplyScript] -eq "alterbars"|&damageRoutine;ApplyDamageAlterbars

    --/|Since we want to be able to hover over a roll and see the dice details, output the rolled damage at the
  --/|top of the card. If all critters make their save, the half damage roll won't contain the details.
  --+|[c][b]Damage Roll: [/b][$Damage][/c]
  --+|&nbsp;
  
  --/|Create an array to hold the tokens that are intersected by the effect area.
  --~|array;define;tokensHit;
  
  --/|Call the "checkForTokenHits" subroutine. The passed parameter will be the per-space VFX. Leave blank for none.
  -->checkForTokenHits|[&breathEffect]

  --/|The first item in the array will be a blank dummy item, so remove it.
  --~|array;removeat;tokensHit;0
  
  --/|Loop through the tokensHit tokens and roll saves for each one and apply damage
  --~tokenid|array;getfirst;tokensHit
  --?[&tokenid] -eq ArrayError|endOutput
  --:loopDisplay|
  --=SaveRoll|1d20 + [*[&tokenid]:[&saveAttributeName]]

  --/|Compare the save roll to the save DC and either apply full or half damage
  --?"[*[&tokenid]:npc_immunities]" -inc "[&damageType]"|Immune
  --?"[*[&tokenid]:npc_resistances]" -inc "[&damageType]"|Resistant
  --?"[*[&tokenid]:npc_vulnerabilities]" -inc "[&damageType]"|Vulnerable
  --?[$SaveRoll.Total] -lt [&breathSaveDC]|>[&damageRoutine];[&tokenid];[&hitPointsBar];-[$Damage.Total]|>[&damageRoutine];[&tokenid];[&hitPointsBar];-[$HalfDamage.Total]
  --?[$SaveRoll.Total] -ge [&breathSaveDC]|madeSave

  --/|Here are various damage applications if the creature is immune, resistant, or vulnerable. In some cases, we will reuse output lines,
  --/|for example, a resistant creature that fails its save will jump to "madeSave", since that is the correct damage amount. (half), while
  --/|a vulnerable creature that makes its save will jump to "FailedSave" since that will be normal damage.

  --/|Output a line for a failed saving throw (we will also jump here for a vulnerable creature that MAKES its save)
  --:FailedSave|
  --+[*[&tokenid]:t-name]:|Save [$SaveRoll] [r][$Damage] [&damageType][/r]
  --^afterSave|
  
  --:Immune|
  --+[*[&tokenid]:t-name]:|is not effected by the spell!
  --^afterSave|

  --:Resistant|
  --?[$SaveRoll.Total] -lt @{selected|spell_save_dc}|>[&damageRoutine];[&tokenid];3;-[$HalfDamage.Total]|>[&damageRoutine];[&tokenid];3;-[$QuarterDamage.Total]
  --?[$SaveRoll.Total] -lt @{selected|spell_save_dc}|madeSave
  --+[*[&tokenid]:t-name]:|Save [$SaveRoll] [r][$QuarterDamage] [&damageType][/r]
  --^afterSave|

  --:Vulnerable|
  --?[$SaveRoll.Total] -lt @{selected|spell_save_dc}|>[&damageRoutine];[&tokenid];[&hitPointsBar];-[$DoubleDamage.Total]|>[&damageRoutine];[&tokenid];[&hitPointsBar];-[$Damage.Total]
  --?[$SaveRoll.Total] -ge @{selected|spell_save_dc}|FailedSave
  --+[*[&tokenid]:t-name]:|Save [$SaveRoll] [r][$DoubleDamage] [&damageType][/r]
  --^afterSave|

  --/|Output a line for a successful saving throw
  --:madeSave|
  --+[*[&tokenid]:t-name]:|Save [$SaveRoll] [r][$HalfDamage] [&damageType][/r]
  --:afterSave|

  --~tokenid|array;getnext;tokensHit
  --?[&tokenid] -ne ArrayError|loopDisplay

  --:endOutput|
  
  --X|
  
  --:ApplyDamageTokenmod|Parameters are tokenid;bar#;amount
  --=newValue|[*[%1%]:t-bar[%2%]_value] - [%3%]
  --?[$newValue] -lt 0|=newValue|0
  --@token-mod|_ignore-selected _ids [%1%] _set bar[%2%]_value|[$newValue.Raw]
  --<| 
  
  --:ApplyDamageAlterbars|Parameters are tokenid;bar#;amount
  --@alter|_target|[%1%] _bar|[%2%] _amount|-[%3%] _show|none
  --<|

  --:DontApplyDamage|
  --<|
  
  --:checkForTokenHits|parameter is vfx descriptor
  --=baseX|[*S:t-left] - 1 \ 70
  --=baseY|[*S:t-top] - 1 \ 70
  --~offset|array;getfirst;[&damageDirection]
  --:checkTokenLoop|
  --?"[&offset]" -eq "ArrayError"|endCheckForTokenHits
  --~split|string;split;,;[&offset]
  --=thisX|[$baseX.Raw] + [&split1]
  --=thisY|[$baseY.Raw] + [&split2]
  --/|IF we passed a visual effect specifier in %1%, create a point VFX in the square
  --?"X[%1%]X" -ne "XX" |[
    --=VX|[$thisX] * 70 + 35   
    --=VY|[$thisY] * 70 + 35   
    --vpoint|[$VX] [$VY] [%1%]
  --]|
  -->checkTokens|[$thisX.Raw];[$thisY.Raw]
  --~offset|array;getnext;[&damageDirection]
  --^checkTokenLoop|
  --:endCheckForTokenHits|
  --<|
    
  --/|Reads the variables created by createTokenLookup to find any tokens that occupy a given space.
  --:checkTokens|x;y
  --?"X[&tok[%1%]-[%2%]]X" -ne "XX"|[
  --~split|string;split;!;[&tok[%1%]-[%2%]]
  --%P|1;[$splitCount.Total];1
  --=var|split[&P]
  --?"X[&[$var.RollText]]X" -eq "XX"|%
  --~exists|array;indexof;tokensHit;[&[$var.RollText]]
  --?[&exists] -ne ArrayError|skipAdd
  --?[&[$var.RollText]] -eq ArrayError|skipAdd
  --~|array;add;tokensHit;[&[$var.RollText]]
  --:skipAdd|
  --%|
  --<|
  
  --/|Creates a series of string variables with names like 'tok12-15' which represent the various squares that are
  --/|occupied by tokens on the page. Handle 1x1, 2x2, and 3x3 tokens by simply adding extra squares in the appropriate
  --/|pattern around the center point location. The result is that if there are two tokens at 12,15, the tok12-15 string
  --/|will contain something like "-asln34njfn2nafd!-sdnfklaserfs" (IDs separated by exclamation points) that we can use to
  --/|quickly evaluate hits by essentially asking "what is in square 12x15" by just reading the variable.
  --:createTokenLookup|
  --~tokenid|array;getfirst;alltokens
  --:tokenSetupLoop|
    --?[&tokenid] -eq ArrayError|endSetupLoop
    --=tLeft|[*[&tokenid]:t-left] - 1 \ 70
    --=tTop|[*[&tokenid]:t-top] - 1 \ 70
    --=tWidth|[*[&tokenid]:t-width]
    --?[$tWidth] -eq 70|[ 
      --&tok[$tLeft.Raw]-[$tTop.Raw]|+[&tokenid]!
      --/+[*[&tokenid]:character_name]|At [$tLeft.Raw],[$tTop.Raw] : [&tok[$tLeft.Raw.Total]-[$tTop.Raw.Total]]
    --]|
  --?[$tWidth] -eq 140|[ 
    --=NX|[$tLeft.Raw] + 1
    --=NY|[$tTop.Raw] + 1
    --&tok[$tLeft.Raw]-[$tTop.Raw]|+[&tokenid]!
    --&tok[$tLeft.Raw]-[$NY]|+[&tokenid]!
    --&tok[$NX]-[$tTop.Raw]|+[&tokenid]!
    --&tok[$NX]-[$NY]|+[&tokenid]!
  --]|
  --?[$tWidth] -eq 210|[ 
    --=PX|[$tLeft.Raw] - 1
    --=PY|[$tTop.Raw] - 1
    --=NX|[$tLeft.Raw] + 1
    --=NY|[$tTop.Raw] + 1
      --&tok[$tLeft.Raw]-[$tTop.Raw]|+[&tokenid]!
      --&tok[$PX.Raw]-[$PY.Raw]|+[&tokenid]!
    --&tok[$tLeft.Raw]-[$PY.Raw]|+[&tokenid]!
    --&tok[$NX]-[$PY.Raw]|+[&tokenid]!
      --&tok[$PX.Raw]-[$tTop.Raw]|+[&tokenid]!
    --&tok[$NX]-[$tTop.Raw]|+[&tokenid]!
      --&tok[$PX.Raw]-[$NY]|+[&tokenid]!
      --&tok[$tLeft.Raw]-[$NY]|+[&tokenid]!
      --&tok[$NX]-[$NY]|+[&tokenid]!
  --]|
  --~tokenid|array;getnext;alltokens
  --^tokenSetupLoop|
  --:endSetupLoop|
  --<|

}}